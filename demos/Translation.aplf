 (rc text)←Translation audioFile;oref;req;resp;h
⍝ This demonstrates using OpenAI to translate an audio file to written English
⍝ audioFile is the path to a previously recorded audio file. .mp3 is preferred
⍝ rc is the return code and is 0 if the translation was successful, otherwise it will be non-0
⍝ text is either the translated text if the translation was successful or a message explaining why the translation failed

 rc←¯1

⍝ Check that OpenAI is present
 :If 9.1≠⎕NC⊂'OpenAI' ⋄ →0⊣text←'OpenAI is not loaded, please run Setup' ⋄ :EndIf

⍝ Make sure the OpenAI.APIKey is set
 :If 0∊⍴OpenAI.APIKey ⋄ →0⊣text←'OpenAPI.APIKey has not been set, please run Setup' ⋄ :EndIf

⍝ Check that the audio file exists
 :If ~⎕NEXISTS audioFile ⋄ →0⊣text←'Audio file "',audioFile,'" not found' ⋄ :EndIf

⍝ create the request
 req←OpenAI.Audio.Translation audioFile

⍝ run the request, capture the response
 resp←req.Run
 :If 0≠resp.rc ⋄ (rc text)←resp.(rc msg)
 :ElseIf 200≠resp.HttpStatus ⋄ (rc text)←(resp.HttpStatus(msg,' - ',HttpMessage))
 :Else ⋄ (rc text)←0 resp.Data.text
 :EndIf
